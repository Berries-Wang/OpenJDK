# invokespecial
&nbsp;&nbsp; 用来调用**特殊**的**实例**方法，包括如下三种：

1. 实例构造器方法 \<init>
2. private修饰的私有实例方法
3. 使用super关键字调用的父类方法

## 疑问： 为什么有了invokevirtual指令还需要invokespecial指令呢？
&nbsp;&nbsp;这是出于效率考虑，invokespecial 调用的方法在编译器是可以确定的，例如private方法是不会因为继承而被子类重写，在编译期间就可以确定，所以private方法的调用使用invokespecial指令。

## 代码示例
### Java代码
```java
public class InvokeSpecial extends Person {

    private String sayHello(String msg) {
        String sayWhat = super.sayWhat(msg);
        return "Hello" + msg;
    }

    public static void main(String[] args) {
        InvokeSpecial invokeSpecial = new InvokeSpecial();
        String hello_world = invokeSpecial.sayHello("Hello World");
    }
}
```

### 字节码
```txt
{
  public org.example.InvokeSpecial();
    descriptor: ()V
    flags: ACC_PUBLIC
    Code:
      stack=1, locals=1, args_size=1
         0: aload_0
         1: invokespecial #1                  // Method org/example/Person."<init>":()V
         4: return
      LineNumberTable:
        line 3: 0
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0       5     0  this   Lorg/example/InvokeSpecial;

  private java.lang.String sayHello(java.lang.String);
    descriptor: (Ljava/lang/String;)Ljava/lang/String;
    flags: ACC_PRIVATE
    Code:
      stack=2, locals=3, args_size=2
         0: aload_0
         1: aload_1
         2: invokespecial #2                  // Method org/example/Person.sayWhat:(Ljava/lang/String;)Ljava/lang/String;
         5: astore_2
         6: new           #3                  // class java/lang/StringBuilder
         9: dup
        10: invokespecial #4                  // Method java/lang/StringBuilder."<init>":()V
        13: ldc           #5                  // String Hello
        15: invokevirtual #6                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        18: aload_1
        19: invokevirtual #6                  // Method java/lang/StringBuilder.append:(Ljava/lang/String;)Ljava/lang/StringBuilder;
        22: invokevirtual #7                  // Method java/lang/StringBuilder.toString:()Ljava/lang/String;
        25: areturn
      LineNumberTable:
        line 6: 0
        line 7: 6
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      26     0  this   Lorg/example/InvokeSpecial;
            0      26     1   msg   Ljava/lang/String;
            6      20     2 sayWhat   Ljava/lang/String;

  public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=2, locals=3, args_size=1
         0: new           #8                  // class org/example/InvokeSpecial
         3: dup
         4: invokespecial #9                  // Method "<init>":()V
         7: astore_1
         8: aload_1
         9: ldc           #10                 // String Hello World
        11: invokespecial #11                 // Method sayHello:(Ljava/lang/String;)Ljava/lang/String;
        14: astore_2
        15: return
      LineNumberTable:
        line 11: 0
        line 12: 8
        line 13: 15
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      16     0  args   [Ljava/lang/String;
            8       8     1 invokeSpecial   Lorg/example/InvokeSpecial;
           15       1     2 hello_world   Ljava/lang/String;
}
```

----
## 参考资料
1. 《深入理解JVM字节码》