# 自动拆箱装箱引发的NPE
1. Long tempVar1 = null == objTempVar? 0L:objTempVar.getTempVar1();
   - 注意，此时若objTempVar.getTempVar1()返回的为null，则会报空指针异常，原因: 对比代码修改前后的字节码即可发现原因

## 代码示例
### 异常代码
#### Java代码
```java
        public class ObjectTem {

            Long a;

            public ObjectTem(Long a) {
                this.a = a;
            }

            public Long getA() {
                return a;
            }

            public void setA(Long a) {
                this.a = a;
            }

        }
    // --------------------
    public static void main(String[] args) {
        ObjectTem objectTem = new ObjectTem(null);
        
        // 运行报错,NPE
        Long tempVar1 = null == objectTem ? 0L : objectTem.getA();

        log.info("[{}]", tempVar1);
    }
```

####  对应字节码
+ 可以看到字节码偏移量第22，这里调用了Long类型实例的java/lang/Long.longValue:()J方法（J代表基本类型long），此时因为getA返回的为NULL,因此报了空指针异常。
    - 将字节码翻译成Java代码如下： 
        + Long tempVar1 = Long.valueOf((null == objectTem) ? 0L : objectTem.getA().longValue());

```txt
    public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=3, locals=3, args_size=1
         0: new           #2                  // class link/bosswang/other/ObjectTem
         3: dup
         4: aconst_null
         5: invokespecial #3                  // Method link/bosswang/other/ObjectTem."<init>":(Ljava/lang/Long;)V
         8: astore_1
         9: aconst_null
        10: aload_1
        11: if_acmpne     18
        14: lconst_0
        15: goto          25
        18: aload_1
        19: invokevirtual #4                  // Method link/bosswang/other/ObjectTem.getA:()Ljava/lang/Long;
        22: invokevirtual #5                  // Method java/lang/Long.longValue:()J
        25: invokestatic  #6                  // Method java/lang/Long.valueOf:(J)Ljava/lang/Long;
        28: astore_2
        29: getstatic     #7                  // Field log:Lorg/slf4j/Logger;
        32: ldc           #8                  // String [{}]
        34: aload_2
        35: invokeinterface #9,  3            // InterfaceMethod org/slf4j/Logger.info:(Ljava/lang/String;Ljava/lang/Object;)V
        40: return
      LineNumberTable:
        line 16: 0
        line 18: 9
        line 20: 29
        line 21: 40
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      41     0  args   [Ljava/lang/String;
            9      32     1 objectTem   Llink/bosswang/other/ObjectTem;
           29      12     2 tempVar1   Ljava/lang/Long;
      StackMapTable: number_of_entries = 2
        frame_type = 252 /* append */
          offset_delta = 18
          locals = [ class link/bosswang/other/ObjectTem ]
        frame_type = 70 /* same_locals_1_stack_item */
          stack = [ long ]
    MethodParameters:
      Name                           Flags
      args

```

### 代码改进后（无NPE）
#### 修改后Java代码
```java
        public class ObjectTem {

            Long a;

            public ObjectTem(Long a) {
                this.a = a;
            }

            public Long getA() {
                return a;
            }

            public void setA(Long a) {
                this.a = a;
            }

        }
    // --------------------
    public static void main(String[] args) {
        ObjectTem objectTem = new ObjectTem(null);
        
        // 运行不报错
        Long tempVar1 = null == objectTem ? (new Long(0L)) : objectTem.getA();

        log.info("[{}]", tempVar1);
    }

```
####  修改后Java代码对应字节码
+ 通过字节码发现，代码执行过程中不会调用Long类型实例的java/lang/Long.longValue:()J方法（J代表基本类型long）

```txt
    public static void main(java.lang.String[]);
    descriptor: ([Ljava/lang/String;)V
    flags: ACC_PUBLIC, ACC_STATIC
    Code:
      stack=4, locals=3, args_size=1
         0: new           #2                  // class link/bosswang/other/ObjectTem
         3: dup
         4: aconst_null
         5: invokespecial #3                  // Method link/bosswang/other/ObjectTem."<init>":(Ljava/lang/Long;)V
         8: astore_1
         9: aconst_null
        10: aload_1
        11: if_acmpne     25
        14: new           #4                  // class java/lang/Long
        17: dup
        18: lconst_0
        19: invokespecial #5                  // Method java/lang/Long."<init>":(J)V
        22: goto          29
        25: aload_1
        26: invokevirtual #6                  // Method link/bosswang/other/ObjectTem.getA:()Ljava/lang/Long;
        29: astore_2
        30: getstatic     #7                  // Field log:Lorg/slf4j/Logger;
        33: ldc           #8                  // String [{}]
        35: aload_2
        36: invokeinterface #9,  3            // InterfaceMethod org/slf4j/Logger.info:(Ljava/lang/String;Ljava/lang/Object;)V
        41: return
      LineNumberTable:
        line 16: 0
        line 18: 9
        line 20: 30
        line 21: 41
      LocalVariableTable:
        Start  Length  Slot  Name   Signature
            0      42     0  args   [Ljava/lang/String;
            9      33     1 objectTem   Llink/bosswang/other/ObjectTem;
           30      12     2 tempVar1   Ljava/lang/Long;
      StackMapTable: number_of_entries = 2
        frame_type = 252 /* append */
          offset_delta = 25
          locals = [ class link/bosswang/other/ObjectTem ]
        frame_type = 67 /* same_locals_1_stack_item */
          stack = [ class java/lang/Long ]
    MethodParameters:
      Name                           Flags
      args
```
